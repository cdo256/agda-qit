open import QIT.Prelude
open import QIT.Relation.Binary
open import QIT.Container.Base

-- Define plump ordinals Z of a given shape. They are used as size
-- bounds on trees for constructing diagrams, and then colimits.
-- This definition was copied from Fiore et al. 2022, and their earlier work (Pitts et al. 2021).
-- Start with an shape and position. This represents the 'shape' of
-- the underlying W-type being constructed.
module QIT.Relation.Plump {‚ÑìS ‚ÑìP} (S : Set ‚ÑìS) (P : S ‚Üí Set ‚ÑìP) where

private
  T = W S P

-- We extend it to have 'enough' points:
--  - ‚ä•À¢ is the shape for a global minimum (P ‚ä•À¢ ‚âÖ ‚ä•).
--  - ‚à®À¢ is the shape for a join operation (P ‚à®À¢ ‚âÖ ùüö).
--  - ŒπÀ¢ s is the shape inlcusion for any shape S.

-- ‚ä•À¢ is required because we need to know that the tree isn't empty,
-- and we can't determine that for an arbitrary W type. It is an
-- algebraic convenience, and not strictly necessary.
-- ‚à®À¢ is required for congruence on œà in cocontinuity, since we need
-- to be able to join two ordinals to a greater ordinal.

-- It is not required that the set of ordinals exactly follow the
-- shape of the W type, they just have to have 'enough' brancing
-- structure to have a natural injection from T to allow for
-- bounding and construction of stage sets.
data S·∂ª : Set ‚ÑìS where
  ‚ä•À¢ : S·∂ª
  ‚à®À¢ : S·∂ª
  ŒπÀ¢ : S ‚Üí S·∂ª

-- Lifting is required, since we want all positions to be at the same
-- level.
P·∂ª : S·∂ª ‚Üí Set ‚ÑìP
P·∂ª ‚ä•À¢ = Lift _ ‚ä•
P·∂ª ‚à®À¢ = Lift _ (‚ä§ ‚äé ‚ä§)
P·∂ª (ŒπÀ¢ s) = P s

Z : Set (‚ÑìS ‚äî ‚ÑìP)
Z = W S·∂ª P·∂ª

‚ä•·∂ª : Z
‚ä•·∂ª = sup (‚ä•À¢ , Œª ())

suc·∂ª : Z ‚Üí Z
suc·∂ª Œ± = sup (‚à®À¢ , Œª _ ‚Üí Œ±)

-- Define branching.
-- Note that both Œ± and Œ≤ are strictly less than Œ± ‚à®·∂ª Œ≤, not less or
-- equal, so this is not strictly a least upper bound.
_‚à®·∂ª_ : Z ‚Üí Z ‚Üí Z
_‚à®·∂ª_ Œ± Œ≤ = sup (‚à®À¢ , f)
  where
  f : P·∂ª ‚à®À¢ ‚Üí W S·∂ª P·∂ª
  f (lift (inj‚ÇÅ tt)) = Œ±
  f (lift (inj‚ÇÇ tt)) = Œ≤

-- Inclusion from the base W type, T, to plump ordinals Z
-- We just recurse over the tree and map each shape s to ŒπÀ¢ s.
Œπ·∂ª : T ‚Üí Z
Œπ·∂ª (sup (s , f)) = sup (ŒπÀ¢ s , Œª Œ± ‚Üí Œπ·∂ª (f Œ±))

-- Define a well-founded order (‚â§, <) on Z to be 'quasi-extensional'
-- (defined later in this file). We defile ‚â§ and < mutually
-- inductively using two rules defined below:
mutual
  infix 4 _‚â§_ _<_
  -- sup‚â§ states that whenever ‚àÄ Œ±. f Œ± < Œ≤, then sup f ‚â§ Œ≤
  -- This gives us 'one-step quasi-extensionality'.
  data _‚â§_ : Z ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP) where
    sup‚â§ : {s : S·∂ª} {f : P·∂ª s ‚Üí Z}
         ‚Üí {Œ± : Z} (f<Œ± : ‚àÄ Œ≤ ‚Üí f Œ≤ < Œ±)
         ‚Üí sup (s , f) ‚â§ Œ±
  -- <sup states that if ‚àÉ Œ±. Œ≤ ‚â§ f Œ±, then Œ≤ < sup f
  -- This means that if any child is at least as large as some ordinal
  -- then the supremum is strictly larger.
  data _<_ : Z ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP) where
    <sup : {s : S·∂ª} {f : P·∂ª s ‚Üí Z}
         ‚Üí (Œ≤ : P·∂ª s) {Œ± : Z}
         ‚Üí (Œ±‚â§fi : Œ± ‚â§ f Œ≤)
         ‚Üí Œ± < sup (s , f)

-- Reflexivity is obtained recursively using <sup followed by sup‚â§, a
-- common pattern reused several times.
‚â§refl : ‚àÄ Œ± ‚Üí Œ± ‚â§ Œ±
‚â§refl (sup (_ , f)) = sup‚â§ (Œª i ‚Üí <sup i (‚â§refl (f i)))

-- Mutually define three notions of transitivity.
-- These must be mutual as each transitivity statement must either
-- expand a branch...
mutual
  ‚â§‚â§ : {Œ± Œ≤ Œ≥ : Z} ‚Üí Œ≤ ‚â§ Œ≥ ‚Üí Œ± ‚â§ Œ≤ ‚Üí Œ± ‚â§ Œ≥
  ‚â§‚â§ Œ≤‚â§Œ≥ (sup‚â§ f<Œ±) = sup‚â§ Œª i ‚Üí ‚â§< Œ≤‚â§Œ≥ (f<Œ± i)

  ‚â§< : {Œ± Œ≤ Œ≥ : Z} ‚Üí Œ≤ ‚â§ Œ≥ ‚Üí Œ± < Œ≤ ‚Üí Œ± < Œ≥
  ‚â§< (sup‚â§ f<Œ±) (<sup i Œ±‚â§fi) = <‚â§ (f<Œ± i) Œ±‚â§fi

  <‚â§ : {Œ± Œ≤ Œ≥ : Z} ‚Üí Œ≤ < Œ≥ ‚Üí Œ± ‚â§ Œ≤ ‚Üí Œ± < Œ≥
  <‚â§ (<sup i Œ±‚â§fi) Œ±‚â§Œ≤ = <sup i (‚â§‚â§ Œ±‚â§fi Œ±‚â§Œ≤)

<‚Üí‚â§ : ‚àÄ{Œ± Œ≤} ‚Üí Œ± < Œ≤ ‚Üí Œ± ‚â§ Œ≤
<‚Üí‚â§ (<sup i (sup‚â§ f<Œ≤)) = sup‚â§ (Œª j ‚Üí <sup i (<‚Üí‚â§ (f<Œ≤ j)))

<sup·∂ª : ‚àÄ {s} x ‚Üí ‚à• P s ‚à• ‚Üí x < sup (ŒπÀ¢ s , Œª _ ‚Üí x)
<sup·∂ª x ‚à£ Œ± ‚à£ = <sup Œ± (‚â§refl x)

<suc·∂ª : ‚àÄ Œ± ‚Üí Œ± < suc·∂ª Œ±
<suc·∂ª = Œª Œ± ‚Üí <sup (lift (inj‚ÇÅ tt)) (‚â§refl Œ±)

_<·µÄ_ : (W S P) ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
t <·µÄ Œ± = Œπ·∂ª t < Œ±

_‚â§·µÄ_ : (W S P) ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
t ‚â§·µÄ Œ± = Œπ·∂ª t ‚â§ Œ±

<< : ‚àÄ{Œ± Œ≤ Œ≥} ‚Üí Œ≤ < Œ≥ ‚Üí Œ± < Œ≤ ‚Üí Œ± < Œ≥
<< (<sup i Œ≤‚â§fi) Œ≤<Œ≥ = <sup i (<‚Üí‚â§ (‚â§< Œ≤‚â§fi Œ≤<Œ≥))

fi‚â§sup : ‚àÄ s f i ‚Üí f i ‚â§ sup (s , f)
fi‚â§sup s f i = <‚Üí‚â§ (<sup i (‚â§refl (f i)))

iswf< : WellFounded _<_
iswf< Œ± = acc Œª Œ≤ Œ≤<Œ± ‚Üí p Œ± Œ≤ (<‚Üí‚â§ Œ≤<Œ±)
  where
  p : ‚àÄ Œ± Œ≤ ‚Üí Œ≤ ‚â§ Œ± ‚Üí Acc _<_ Œ≤
  p (sup (_ , f)) Œ≤ Œ≤‚â§Œ± = acc q
    where
    q : WfRec _<_ (Acc _<_) Œ≤
    q Œ≥ Œ≥<Œ≤ with ‚â§< Œ≤‚â§Œ± Œ≥<Œ≤
    ... | <sup i Œ≥‚â§fi = p (f i) Œ≥ Œ≥‚â§fi

isPreorder-‚â§ : IsPreorder _‚â§_
isPreorder-‚â§ = record
  { refl = Œª {x} ‚Üí ‚â§refl x
  ; trans = Œª p q ‚Üí ‚â§‚â§ q p }

‚â§p : Preorder (W S·∂ª P·∂ª) _
‚â§p = _‚â§_ , isPreorder-‚â§

_‚äÜ_ : Z ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
Œ± ‚äÜ Œ≤ = ‚àÄ Œ≥ ‚Üí Œ≥ < Œ± ‚Üí Œ≥ < Œ≤

_‚äá_ : Z ‚Üí Z ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
Œ± ‚äá Œ≤ = ‚àÄ Œ≥ ‚Üí Œ± < Œ≥ ‚Üí Œ≤ < Œ≥

‚äÜ‚Üí‚â§ : ‚àÄ {Œ± Œ≤} ‚Üí Œ± ‚äÜ Œ≤ ‚Üí Œ± ‚â§ Œ≤
‚äÜ‚Üí‚â§ {sup (s , f)} {sup (t , g)} p =
  sup‚â§ (Œª x ‚Üí p (f x) (<sup x (‚â§refl (f x))))

‚â§‚Üí‚äÜ : ‚àÄ {Œ± Œ≤} ‚Üí Œ± ‚â§ Œ≤ ‚Üí Œ± ‚äÜ Œ≤
‚â§‚Üí‚äÜ {sup (s , f)} {sup (t , g)} sf‚â§tg =
  Œª Œ≥ Œ≥<sf ‚Üí ‚â§< sf‚â§tg Œ≥<sf

‚â§‚Üí‚äá : ‚àÄ {Œ± Œ≤} ‚Üí Œ± ‚â§ Œ≤ ‚Üí Œ≤ ‚äá Œ±
‚â§‚Üí‚äá Œ±‚â§Œ≤ _ Œ≤<Œ≥ = <‚â§ Œ≤<Œ≥ Œ±‚â§Œ≤

_‚â§‚â•_ : ‚àÄ (x y : W S·∂ª P·∂ª) ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
x ‚â§‚â• y = (x ‚â§ y) ‚àß (y ‚â§ x)
_‚äÜ‚äá_ : ‚àÄ (x y : W S·∂ª P·∂ª) ‚Üí Prop (‚ÑìS ‚äî ‚ÑìP)
x ‚äÜ‚äá y = (x ‚äÜ y) ‚àß (y ‚äÜ x)

isQuasiExtensionalZ : ‚àÄ {x y} ‚Üí (x ‚â§‚â• y) ‚áî (x ‚äÜ‚äá y)
isQuasiExtensionalZ = (Œª (Œ±‚â§Œ≤ , Œ≤‚â§Œ±) ‚Üí ‚â§‚Üí‚äÜ Œ±‚â§Œ≤ , ‚â§‚Üí‚äÜ Œ≤‚â§Œ±) , Œª (Œ±‚äÜŒ≤ , Œ≤‚äÜŒ±) ‚Üí ‚äÜ‚Üí‚â§ Œ±‚äÜŒ≤ , ‚äÜ‚Üí‚â§ Œ≤‚äÜŒ±

‚â§cong : ‚àÄ s (Œº œÑ : P·∂ª s ‚Üí Z) ‚Üí (r : ‚àÄ i ‚Üí Œº i ‚â§ œÑ i)
      ‚Üí sup (s , Œº) ‚â§ sup (s , œÑ)
‚â§cong s Œº œÑ r = sup‚â§ Œª i ‚Üí <sup i (r i)

‚à®·∂ª-l< : {Œ± Œ≤ : Z} ‚Üí Œ± < Œ± ‚à®·∂ª Œ≤
‚à®·∂ª-l< {Œ±} {Œ≤} = <sup (lift (inj‚ÇÅ tt)) (‚â§refl Œ±)

‚à®·∂ª-r< : {Œ± Œ≤ : Z} ‚Üí Œ≤ < Œ± ‚à®·∂ª Œ≤
‚à®·∂ª-r< {Œ±} {Œ≤} = <sup (lift (inj‚ÇÇ tt)) (‚â§refl Œ≤)

‚à®·∂ª-l : {Œ± Œ≤ : Z} ‚Üí Œ± ‚â§ Œ± ‚à®·∂ª Œ≤
‚à®·∂ª-l = fi‚â§sup ‚à®À¢ _ (lift (inj‚ÇÅ tt))

‚à®·∂ª-r : {Œ± Œ≤ : Z} ‚Üí Œ≤ ‚â§ Œ± ‚à®·∂ª Œ≤
‚à®·∂ª-r = fi‚â§sup ‚à®À¢ _ (lift (inj‚ÇÇ tt))

‚à®·∂ª-flip : {Œ± Œ≤ : Z} ‚Üí Œ≤ ‚à®·∂ª Œ± ‚â§ Œ± ‚à®·∂ª Œ≤
‚à®·∂ª-flip {Œ±} {Œ≤} = sup‚â§ g
  where
  g : (i : P·∂ª ‚à®À¢) ‚Üí _ < (Œ± ‚à®·∂ª Œ≤)
  g (lift (inj‚ÇÅ tt)) = <sup (lift (inj‚ÇÇ tt)) (‚â§refl Œ≤)
  g (lift (inj‚ÇÇ tt)) = <sup (lift (inj‚ÇÅ tt)) (‚â§refl Œ±)
