open import QIT.Prelude
open import QIT.Prop
open import QIT.Setoid
open import QIT.Relation.Base
open import QIT.Relation.Binary
open import QIT.Relation.Subset
open import QIT.Container.Base
open import QIT.Functor.Base
open import QIT.Functor.Properties
open import QIT.Category.Base hiding (_[_≈_]; _[_,_]; _[_∘_])
open import QIT.Category.Preorder
open import QIT.Category.Set
open import QIT.Setoid.Quotient
open import QIT.Set.Bijection
open import QIT.QW.Signature

module QIT.QW.Cocontinuity2 {ℓS ℓP ℓE ℓV} (sig : Sig ℓS ℓP ℓE ℓV) where
open Sig sig

private
  ℓD = ℓS ⊔ ℓP
  ℓD' = ℓS ⊔ ℓP ⊔ ℓE ⊔ lsuc ℓV

open import QIT.Container.Base
open import QIT.Container.StrictFunctor S P (ℓD ⊔ ℓD')
open import QIT.Category.Morphism (SetCat (ℓD ⊔ ℓD'))

open import QIT.Plump.Postulated S P as Z hiding (rec)
open import QIT.QW.Stage sig
open import QIT.QW.Algebra sig
open import QIT.QW.StageColimit sig
open import QIT.QW.Colimit ≤p ℓD ℓD' hiding (_≈ˡ_)

private
  ℓc = ℓS ⊔ ℓD
  ℓc' = ℓS ⊔ ℓP ⊔ ℓD ⊔ ℓD'

Cocontinuous : (F : Functor (SetCat (ℓc ⊔ ℓc')) (SetCat (ℓc ⊔ ℓc'))) (D : Diagram/≈ ℓc ℓc')
              → Prop ℓc'
Cocontinuous F D =
  Colim/≈ (F ∘ D) ≅ Functor.ob F (Colim/≈ D)

module ColimF∘D = SetoidQuotient (Colim (F ∘ D))
module ColimD = SetoidQuotient (Colim D)
module Ob = Functor F

module PreservationByPowers
       (depth-preserving : ∀ α ŝ t̂ → α ⊢ ŝ ≈ᵇ t̂ → ιᶻ (ŝ .fst) ≡ ιᶻ (t̂ .fst)) (s : S)
       where

  open SetoidQuotient

  rankD₀ : ∀ {α} → D₀ α → Z
  rankD₀ (t , _) = ιᶻ t

  rankD-cong : ∀ {α ŝ t̂} → α ⊢ ŝ ≈ᵇ t̂ → rankD₀ ŝ ≡ rankD₀ t̂
  rankD-cong {α} {ŝ} {t̂} p = depth-preserving α ŝ t̂ p

  rankD : ∀ {α} → D̃ α /≈ → Z
  rankD {α} = rec (D̃ α) rankD₀ rankD-cong

  rankD-beta : ∀ {α} (t̂ : D₀ α) → rankD (D̃ α ⊢[ t̂ ]) ≡ rankD₀ t̂
  rankD-beta t̂ = ≡.refl

  rankC : Colim/≈ D → Z
  rankC = rec (Colim D) (λ (_ , t̂) → rankD t̂) stable
    where
    stable : ∀ {x y} → Colim D [ x ≈ y ] → rankD (x .proj₂) ≡ rankD (y .proj₂)
    stable (≈lstage i p) = ≡.cong rankD p
    stable (≈lstep {α} {β} p x) =
      elimp (D̃ α)
            (λ q → rankD q ≡ rankD (D/≈.hom (box p) q))
            (λ _ → ≡.refl)
            x
    stable (≈lsym p) = ≡.sym (stable p)
    stable (≈ltrans p q) = ≡.trans (stable p) (stable q)

  plift : ∀ {α} → (t̂ : D₀ α) → D₀ (rankD₀ t̂)
  plift (t , _) = t , ≤refl (ιᶻ t)

  same-stage : ∀ {α} {t : T} (p q : t ≤ᵀ α) → D̃ α [ (t , p) ≈ (t , q) ]
  same-stage p q = ≡→≈ (D̃ _) (ΣP≡ (_ , p) (_ , q) ≡.refl)

  plift≈ : ∀ {α} → (t̂ : D̃ α /≈) → D̃ (rankD t̂) /≈
  plift≈ {α} t̂ = subst (λ β → D̃ β /≈) (first t̂) ((d t̂) .proj₂)
    where
    module Dα = SetoidQuotient (D̃ α)
    open ≡.≡-Reasoning

    castʳ : ∀ {γ} {x y z : D₀ γ} → y ≡ z → D̃ γ [ x ≈ y ] → D̃ γ [ x ≈ z ]
    castʳ ≡.refl p = p

    castˡ : ∀ {γ} {x y z : D₀ γ} → x ≡ y → D̃ γ [ x ≈ z ] → D̃ γ [ y ≈ z ]
    castˡ ≡.refl p = p

    cast-rhs : ∀ {γ s t} {ps : s ≤ᵀ γ} {pt qt : t ≤ᵀ γ}
      → D̃ γ [ (s , ps) ≈ (t , pt) ]
      → D̃ γ [ (s , ps) ≈ (t , qt) ]
    cast-rhs {pt = pt} {qt = qt} p = ≈ptrans p (same-stage pt qt)

    transport≈ : ∀ {γ δ} (p : γ ≡ δ) {x y : D₀ γ}
      → D̃ γ [ x ≈ y ] → D̃ δ [ subst D₀ p x ≈ subst D₀ p y ]
    transport≈ ≡.refl r = r

    subst-D₀-fst : ∀ {γ δ} (p : γ ≡ δ) (û : D₀ γ) → (subst D₀ p û) .fst ≡ û .fst
    subst-D₀-fst ≡.refl û = ≡.refl

    plift-fst : ∀ {γ} (û : D₀ γ) → (plift û) .fst ≡ û .fst
    plift-fst û = ≡.refl

    plift-psup : ∀ a μ (f : ∀ i → D₀ (μ i))
      → plift (psup a μ f) ≡ psup a (λ i → rankD₀ (f i)) (λ i → plift (f i))
    plift-psup a μ f = ΣP≡ _ _ (≡.refl)

    exactify : ∀ {γ} {ŝ t̂ : D₀ γ} (p : D̃ γ [ ŝ ≈ t̂ ])
      → D̃ (rankD₀ ŝ) [ plift ŝ ≈ subst D₀ (≡.sym (rankD-cong p)) (plift t̂) ]
    exactify (≈pcong a μ f₁ g r) = castˡ (plift-psup a μ f₁) (castʳ rhs≈ base)
      where
      δi : ∀ i → rankD₀ (f₁ i) ≡ rankD₀ (g i)
      δi i = rankD-cong (r i)
      μ' : P a → Z
      μ' i = rankD₀ (f₁ i)
      f' : ∀ i → D₀ (μ' i)
      f' i = plift (f₁ i)
      g' : ∀ i → D₀ (μ' i)
      g' i = subst D₀ (≡.sym (δi i)) (plift (g i))
      base : D̃ (rankD₀ (psup a μ f₁)) [ psup a μ' f' ≈ psup a μ' g' ]
      base = ≈pcong a μ' f' g' (λ i → exactify (r i))
      dp : rankD₀ (psup a μ f₁) ≡ rankD₀ (psup a μ g)
      dp = rankD-cong (≈pcong a μ f₁ g r)
      g'fst : ∀ i → (g' i) .fst ≡ (plift (g i)) .fst
      g'fst i = subst-D₀-fst (≡.sym (δi i)) (plift (g i))
      rhs≈ : psup a μ' g' ≡ subst D₀ (≡.sym dp) (plift (psup a μ g))
      rhs≈ = ΣP≡ _ _ rhsfst
        where
        rhsfst : (psup a μ' g') .fst ≡ (subst D₀ (≡.sym dp) (plift (psup a μ g))) .fst
        rhsfst = ≡.trans (≡.cong (λ h → W.sup (a , h)) (≡.funExt g'fst))
                          (≡.sym (subst-D₀-fst (≡.sym dp) (plift (psup a μ g))))
    exactify (≈psat e ϕ l≤α r≤α) = castʳ rhs≈ base
      where
      dp : rankD₀ (lhs' e ϕ , l≤α) ≡ rankD₀ (rhs' e ϕ , r≤α)
      dp = rankD-cong (≈psat e ϕ l≤α r≤α)
      base : D̃ (rankD₀ (lhs' e ϕ , l≤α)) [ plift (lhs' e ϕ , l≤α) ≈ (rhs' e ϕ , ≡.substp (rhs' e ϕ ≤ᵀ_) (≡.sym dp) (≤refl _)) ]
      base = ≈psat e ϕ (≤refl _) (≡.substp (rhs' e ϕ ≤ᵀ_) (≡.sym dp) (≤refl _))
      rhs≈ : (rhs' e ϕ , ≡.substp (rhs' e ϕ ≤ᵀ_) (≡.sym dp) (≤refl _)) ≡ subst D₀ (≡.sym dp) (plift (rhs' e ϕ , r≤α))
      rhs≈ = ΣP≡ _ _ (≡.sym (subst-D₀-fst (≡.sym dp) (plift (rhs' e ϕ , r≤α))))
    exactify ≈prefl = ≈prefl
    exactify {ŝ = ŝ} {t̂ = t̂} (≈psym p) =
      castˡ {z = subst D₀ dp (plift t̂)} lhs≈ (transport≈ dp (≈psym (exactify p)))
      where
      dp : rankD₀ t̂ ≡ rankD₀ ŝ
      dp = rankD-cong p
      lhs≈ : subst D₀ dp (subst D₀ (≡.sym dp) (plift ŝ)) ≡ plift ŝ
      lhs≈ = ≡.subst-inv D₀ (≡.sym dp)
    exactify {ŝ = ŝ} {t̂ = û} (≈ptrans {ŝ = ŝ} {t̂ = t̂} {û = û} p q) = castʳ rhs≈ (≈ptrans (exactify p) mid)
      where
      dp : rankD₀ ŝ ≡ rankD₀ t̂
      dp = rankD-cong p
      dq : rankD₀ t̂ ≡ rankD₀ û
      dq = rankD-cong q
      mid : D̃ (rankD₀ ŝ) [ subst D₀ (≡.sym dp) (plift t̂) ≈ subst D₀ (≡.sym dp) (subst D₀ (≡.sym dq) (plift û)) ]
      mid = transport≈ (≡.sym dp) (exactify q)
      rhs≈ : subst D₀ (≡.sym dp) (subst D₀ (≡.sym dq) (plift û)) ≡ subst D₀ (≡.sym (rankD-cong (≈ptrans p q))) (plift û)
      rhs≈ = ≡.subst-subst {P = D₀} (≡.sym dq) {y≡z = ≡.sym dp} {p = plift û}
    exactify (≈pweaken α≤β p) = exactify p

    f : D₀ α → Σ Z (λ β → D̃ β /≈)
    f t̂ = rankD₀ t̂ , D̃ (rankD₀ t̂) ⊢[ plift t̂ ]

    u : ∀ {γ δ} {û : D₀ δ} (p : γ ≡ δ)
      → subst (λ β → D̃ β /≈) p (D̃ γ ⊢[ subst D₀ (≡.sym p) û ])
      ≡ D̃ δ ⊢[ û ]
    u ≡.refl = ≡.refl

    f-cong : ∀ {ŝ t̂ : D₀ α} → (p : D̃ α [ ŝ ≈ t̂ ]) → f ŝ ≡ f t̂
    f-cong {ŝ} {t̂} p = ≡.Σ≡ dp q
      where
      dp : rankD₀ ŝ ≡ rankD₀ t̂
      dp = rankD-cong p
      q : subst (λ β → D̃ β /≈) dp (D̃ (rankD₀ ŝ) ⊢[ plift ŝ ])
        ≡ D̃ (rankD₀ t̂) ⊢[ plift t̂ ]
      q = begin
          subst (λ β → D̃ β /≈) dp (D̃ (rankD₀ ŝ) ⊢[ plift ŝ ])
            ≡⟨ ≡.cong (subst (λ β → D̃ β /≈) dp) (D̃ (rankD₀ ŝ) ⊢≈[ exactify p ]) ⟩
          subst (λ β → D̃ β /≈) dp (D̃ (rankD₀ ŝ) ⊢[ subst D₀ (≡.sym dp) (plift t̂) ])
            ≡⟨ u dp ⟩
          D̃ (rankD₀ t̂) ⊢[ plift t̂ ] ∎

    d : D̃ α /≈ → Σ Z (λ β → D̃ β /≈)
    d = rec (D̃ α) f f-cong

    first : ∀ t̂ → (d t̂) .proj₁ ≡ rankD t̂
    first = elimp (D̃ α) (λ t̂ → (d t̂) .proj₁ ≡ rankD t̂) (λ _ → ≡.refl)

  s≤rankD : ∀ {α} (t̂ : D₀ α) → t̂ .fst ≤ᵀ rankD (D̃ α ⊢[ t̂ ])
  s≤rankD {α} t̂ = ≤refl (rankD₀ t̂)

  _~ᵀ_ : ∀ (s t : T) → Prop _
  s ~ᵀ t = ιᶻ s ≡ ιᶻ t

  _~⁰_ : ∀ {α β} → D₀ α → D₀ β → Prop _
  (s , _) ~⁰ (t , _) = s ~ᵀ t

  _~∀_ : ∀ {α β} → D̃ α /≈ → D̃ β /≈ → Prop _
  _~∀_ = QuotHetRel∀ D̃ _~⁰_

  _~∃_ : ∀ {α β} → D̃ α /≈ → D̃ β /≈ → Prop _
  _~∃_ = QuotHetRel∃ D̃ _~⁰_

  ~∀→∃ : ∀ {α β} (ŝ : D̃ α /≈) (t̂ : D̃ β /≈) → ŝ ~∀ t̂ → ŝ ~∃ t̂
  ~∀→∃ = QuotHetRel∀→∃ D̃ _~⁰_

  ~∃→∀ : ∀ {α β} (ŝ : D̃ α /≈) (t̂ : D̃ β /≈) → ŝ ~∃ t̂ → ŝ ~∀ t̂
  ~∃→∀ {α} {β} ŝ t̂ ∣ qrwitness (s' , s'≤α) (t' , t'≤β) r ps' pt' ∣ (s , s≤α) (t , t≤β) ps pt = p
    where
    module Dα = SetoidQuotient (D̃ α)
    module Dβ = SetoidQuotient (D̃ β)

    rs : D̃ α [ (s , s≤α) ≈ (s' , s'≤α) ]
    rs = Dα.effectiveness _ _ (≡.trans ps (≡.sym ps'))

    rt : D̃ β [ (t' , t'≤β) ≈ (t , t≤β) ]
    rt = Dβ.effectiveness _ _ (≡.trans pt' (≡.sym pt))

    qs : s ~ᵀ s'
    qs = depth-preserving α (s , s≤α) (s' , s'≤α) rs

    qt : t' ~ᵀ t
    qt = depth-preserving β (t' , t'≤β) (t , t≤β) rt

    p : (s , s≤α) ~⁰ (t , t≤β)
    p = ≡.trans qs (≡.trans r qt)

  ~⇔ : ∀ {α β} (ŝ : D̃ α /≈) (t̂ : D̃ β /≈) → (ŝ ~∀ t̂) ⇔ (ŝ ~∃ t̂)
  ~⇔ ŝ t̂ = ~∀→∃ ŝ t̂ , ~∃→∀ ŝ t̂

  _~ᶜ⁰_ : Colim₀ D → Colim₀ D → Prop _
  (α , ŝ) ~ᶜ⁰ (β , t̂) = ŝ ~∀ t̂

  _~ᶜ∃⁰_ : Colim₀ D → Colim₀ D → Prop _
  (α , ŝ) ~ᶜ∃⁰ (β , t̂) = ŝ ~∃ t̂

  _~ᶜ∀_ : Colim/≈ D → Colim/≈ D → Prop _
  _~ᶜ∀_ = QuotHomRel∀ (Colim D) _~ᶜ⁰_

  _~ᶜ∃_ : Colim/≈ D → Colim/≈ D → Prop _
  _~ᶜ∃_ = QuotHomRel∃ (Colim D) _~ᶜ⁰_

  ~ᶜ∀→∃ : ∀ (x y : Colim/≈ D) → x ~ᶜ∀ y → x ~ᶜ∃ y
  ~ᶜ∀→∃ = QuotHomRel∀→∃ (Colim D) _~ᶜ⁰_

  ~ᶜ∃→∀ : ∀ (x y : Colim/≈ D) → x ~ᶜ∃ y → x ~ᶜ∀ y
  ~ᶜ∃→∀ x y ∣ qrwitness (α , x₀) (β , y₀) r px py ∣
    (α' , ŝ) (β' , t̂) ≡.refl ≡.refl
    (s , s≤) (t , t≤) ≡.refl ≡.refl =
      ≡.trans (rep-rank (s , s≤) ≡.refl)
        (≡.trans mid (≡.sym (rep-rank (t , t≤) ≡.refl)))
    where
    open Setoid (Colim D)
    open ≈.≈syntax {S = Colim D}

    rank~ : ∀ {γ δ} {û : D̃ γ /≈} {v̂ : D̃ δ /≈} → û ~∀ v̂ → rankD û ≡ rankD v̂
    rank~ {γ} {δ} {û} {v̂} u~v = Dγ.elimp Pred f û v̂ u~v
      where
      module Dγ = SetoidQuotient (D̃ γ)
      module Dδ = SetoidQuotient (D̃ δ)

      Pred : D̃ γ /≈ → Prop _
      Pred û = ∀ v̂ → û ~∀ v̂ → rankD û ≡ rankD v̂

      f : ∀ u₀ → Pred (D̃ γ ⊢[ u₀ ])
      f u₀ v̂ = Dδ.elimp Pred' g v̂
        where
        Pred' : D̃ δ /≈ → Prop _
        Pred' v̂ = (D̃ γ ⊢[ u₀ ]) ~∀ v̂ → rankD (D̃ γ ⊢[ u₀ ]) ≡ rankD v̂

        g : ∀ v₀ → Pred' (D̃ δ ⊢[ v₀ ])
        g v₀ p = ≡.trans (rankD-beta u₀)
                 (≡.trans (p u₀ v₀ ≡.refl ≡.refl)
                           (≡.sym (rankD-beta v₀)))

    rep-rank : ∀ {γ} (u₀ : D₀ γ) {û : D̃ γ /≈}
      → D̃ γ ⊢[ u₀ ] ≡ û → rankD₀ u₀ ≡ rankD û
    rep-rank u₀ pu = ≡.trans (≡.sym (rankD-beta u₀)) (≡.cong rankD pu)

    rank≈ : ∀ {γ δ} {û : D̃ γ /≈} {v̂ : D̃ δ /≈}
      → Colim D [ γ , û ≈ δ , v̂ ] → rankD û ≡ rankD v̂
    rank≈ (≈lstage i e) = ≡.cong rankD e
    rank≈ (≈lstep {i = γ} p û) =
      elimp (D̃ γ)
            (λ q → rankD q ≡ rankD (D/≈.hom (box p) q))
            (λ _ → ≡.refl)
            û
    rank≈ (≈lsym p) = ≡.sym (rank≈ p)
    rank≈ (≈ltrans p q) = ≡.trans (rank≈ p) (rank≈ q)

    ŝ≈x₀ : Colim D [ α' , ŝ ≈ α , x₀ ]
    ŝ≈x₀ = begin
      α' , ŝ
        ≈⟨ ColimD.effectiveness (α' , ŝ) (α , x₀) (≡.sym px) ⟩
      α , x₀ ∎

    y₀≈t̂ : Colim D [ β , y₀ ≈ β' , t̂ ]
    y₀≈t̂ = begin
      β , y₀
        ≈⟨ ColimD.effectiveness (β , y₀) (β' , t̂) py ⟩
      β' , t̂ ∎

    mid : rankD ŝ ≡ rankD t̂
    mid = ≡.trans (rank≈ ŝ≈x₀) (≡.trans (rank~ r) (rank≈ y₀≈t̂))

  ~ᶜ⇔ : ∀ (x y : Colim/≈ D) → (x ~ᶜ∀ y) ⇔ (x ~ᶜ∃ y)
  ~ᶜ⇔ x y = ~ᶜ∀→∃ x y , ~ᶜ∃→∀ x y

  X = P s
  D^X : Diagram/≈ ℓc ℓc'
  D^X = _^_ {ℓc} {ℓc'} D (Lift ℓS X)
  module D^X = Functor D^X
  module ColimD^X = SetoidQuotient (Colim D^X)

  ϕ₀ : Colim₀ D^X → X → Colim₀ D
  ϕ₀ (α , t̂) x = α , t̂ (lift x)

  ϕ-cong : ∀ {t̃ ũ} → Colim D^X [ t̃ ≈ ũ ] → (x : X) → Colim D [ ϕ₀ t̃ x ≈ ϕ₀ ũ x ]
  ϕ-cong {α , t̂} {α , t̂} (≈lstage α ≡.refl) x = ≡→≈ (Colim D) ≡.refl
  ϕ-cong {α , t̂} {β , û} (≈lstep p t̂) x = ≈lstep p (t̂ (lift x))
  ϕ-cong {α , t̂} {β , û} (≈lsym p) x = ≈lsym (ϕ-cong p x)
  ϕ-cong {α , t̂} {β , û} (≈ltrans p q) x = ≈ltrans (ϕ-cong p x) (ϕ-cong q x)

  ϕ : Colim/≈ D^X → (X → Colim/≈ D)
  ϕ f̃ x = ColimD^X.map (Colim D) (λ f → ϕ₀ f x) (λ p → ϕ-cong p x) f̃

  module _ {α β : Z} (α≤β : α ≤ β) where
    module Bα = Bounded D α
    module Bβ = Bounded D β

    map≤₀ : Bα.Colim≤₀ → Bβ.Colim≤₀
    map≤₀ (i≤α , x) = (i≤α .fst , ≤≤ α≤β (i≤α .snd)) , x

    map≈≤ : ∀ {s t} → Bα._≈ˡ≤_ s t → Bβ._≈ˡ≤_ (map≤₀ s) (map≤₀ t)
    map≈≤ (Bα.≈l≤stage ι e) = Bβ.≈l≤stage (ι .fst , ≤≤ α≤β (ι .snd)) e
    map≈≤ (Bα.≈l≤step p x) = Bβ.≈l≤step p x
    map≈≤ (Bα.≈l≤sym r) = Bβ.≈l≤sym (map≈≤ r)
    map≈≤ (Bα.≈l≤trans r₁ r₂) = Bβ.≈l≤trans (map≈≤ r₁) (map≈≤ r₂)

  module _ where
    open Bounded D renaming (_≈ˡ≤_ to _⊢_≈ˡ≤_)
    sameBounded : ∀ {γ α} (p q : α ≤ γ) {y : D̃ α /≈}
      → γ ⊢ ((α , p) , y) ≈ˡ≤ ((α , q) , y)
    sameBounded {γ} {α} p q {y} = B.≈l≤trans (B.≈l≤step (≤refl α) y) (B.≈l≤stage (α , q) eq)
      where
      module B = Bounded D γ
      module Dα = SetoidQuotient (D̃ α)
      module D∣γ = Functor (RestrictDiagram D γ)
      hom-refl : (y : D̃ α /≈) → D∣γ.hom {α , p} {α , p} (box (≤refl α)) y ≡ y
      hom-refl = Dα.elimp (λ y → D∣γ.hom {α , p} {α , p} (box (≤refl α)) y ≡ y) h
        where
        h : ∀ t̂ → D∣γ.hom {α , p} {α , p} (box (≤refl α)) (D̃ α ⊢[ t̂ ]) ≡ D̃ α ⊢[ t̂ ]
        h t̂ = D̃ α ⊢≈[ same-stage _ _ ]
      eq : D∣γ.hom {α , p} {α , p} (box (≤refl α)) y ≡ y
      eq = hom-refl y
        
    record BoundedJoin (x y : Colim₀ D) : Set (ℓS ⊔ ℓP ⊔ lsuc ℓV ⊔ ℓE) where
      constructor bjoin
      private
        α = x .proj₁
        β = y .proj₁
        s̃ = x .proj₂
        t̃ = y .proj₂
      field
        γ : Z
        α≤γ : α ≤ γ
        β≤γ : β ≤ γ
        γ⊢x≈y : γ ⊢ ((α , α≤γ) , s̃) ≈ˡ≤ ((β , β≤γ) , t̃)

    boundedJoin : ∀ {α β} {x : D̃ α /≈} {y : D̃ β /≈}
      → Colim D [ α , x ≈ β , y ]
      → ∥ BoundedJoin (α , x) (β , y) ∥
    boundedJoin = recˡ D C sC pC syC trC
      where
      C : ∀ {s t} → Colim D [ s ≈ t ] → Prop _
      C {α , x} {β , y} _ = ∥ BoundedJoin (α , x) (β , y) ∥ 

      sC : ∀ α {x x'} (e : x ≡ x') → C (≈lstage α e)
      sC α e = ∣ bjoin α (≤refl α) (≤refl α) (≈l≤stage (α , ≤refl α) e) ∣

      pC : ∀ {α β} (p : α ≤ β) (x : D̃ α /≈) → C (≈lstep p x)
      pC {α} {β} p x = ∣ bjoin β p (≤refl β) (≈l≤step p x) ∣

      syC : ∀ {s t} (r : Colim D [ s ≈ t ]) → C r → C (≈lsym r)
      syC {α , x} {β , y} r ∣ bjoin γ α≤γ β≤γ γ⊢x≈y ∣ = ∣ bjoin γ β≤γ α≤γ (≈l≤sym γ⊢x≈y) ∣

      trC : ∀ {s t u} (r₁ : Colim D [ s ≈ t ]) (r₂ : Colim D [ t ≈ u ]) → C r₁ → C r₂ → C (≈ltrans r₁ r₂)
      trC {α , x} {β , y} {δ , z} r₁ r₂
          ∣ bjoin γ₁ α≤γ₁ β≤γ₁ γ₁⊢x≈y ∣
          ∣ bjoin γ₂ β≤γ₂ δ≤γ₂ γ₂⊢y≈z ∣ =
        ∣ bjoin γ α≤γ δ≤γ (≈l≤trans γ⊢x≈y (≈l≤trans γ⊢y≈y γ⊢y≈z)) ∣
        where
        γ : Z
        γ = γ₁ ∨ᶻ γ₂

        α≤γ : α ≤ γ
        α≤γ = ≤≤ ∨ᶻ-l α≤γ₁

        δ≤γ : δ ≤ γ
        δ≤γ = ≤≤ ∨ᶻ-r δ≤γ₂

        β≤γˡ : β ≤ γ
        β≤γˡ = ≤≤ ∨ᶻ-l β≤γ₁

        β≤γʳ : β ≤ γ
        β≤γʳ = ≤≤ ∨ᶻ-r β≤γ₂

        γ⊢x≈y : γ ⊢ ((α , α≤γ) , x) ≈ˡ≤ ((β , β≤γˡ) , y)
        γ⊢x≈y = map≈≤ ∨ᶻ-l γ₁⊢x≈y

        γ⊢y≈z : γ ⊢ ((β , β≤γʳ) , y) ≈ˡ≤ ((δ , δ≤γ) , z)
        γ⊢y≈z = map≈≤ ∨ᶻ-r γ₂⊢y≈z

        γ⊢y≈y : γ ⊢ ((β , β≤γˡ) , y) ≈ˡ≤ ((β , β≤γʳ) , y)
        γ⊢y≈y = sameBounded β≤γˡ β≤γʳ

    rankColim : ∀ {γ δ} {x : D̃ γ /≈} {y : D̃ δ /≈}
              → Colim D [ γ , x ≈ δ , y ] → rankD x ≡ rankD y
    rankColim (≈lstage i e) = ≡.cong rankD e
    rankColim (≈lstep {i = γ} p x) =
      elimp (D̃ γ)
            (λ q → rankD q ≡ rankD (D/≈.hom (box p) q))
            (λ _ → ≡.refl)
            x
    rankColim (≈lsym p) = ≡.sym (rankColim p)
    rankColim (≈ltrans p q) = ≡.trans (rankColim p) (rankColim q)

    sameHom : ∀ {α γ} (p q : α ≤ γ) {x : D̃ α /≈}
            → D.hom (box p) x ≡ D.hom (box q) x
    sameHom {α} {γ} p q {x} = Dα.elimp B h x
      where
      module Dα = SetoidQuotient (D̃ α)
      B : D̃ α /≈ → Prop _
      B x = D.hom (box p) x ≡ D.hom (box q) x
      h : ∀ û → B (D̃ α ⊢[ û ])
      h (t , t≤α) = D̃ γ ⊢≈[ same-stage (≤≤ p t≤α) (≤≤ q t≤α) ]

    rankD≤stage : ∀ {α} (x : D̃ α /≈) → rankD x ≤ α
    rankD≤stage {α} = elimp (D̃ α) (λ x → rankD x ≤ α) (λ (t , t≤α) → t≤α)

    toRankHom : ∀ {α} (x : D̃ α /≈) → ∀ {γ} (α≤γ : α ≤ γ)
              → D.hom (box α≤γ) x
              ≡ D.hom (box (≤≤ α≤γ (rankD≤stage x))) (plift≈ x)
    toRankHom {α} x {γ} α≤γ = Dα.elimp B h x
      where
      module Dα = SetoidQuotient (D̃ α)
      B : D̃ α /≈ → Prop _
      B x = D.hom (box α≤γ) x
          ≡ D.hom (box (≤≤ α≤γ (rankD≤stage x))) (plift≈ x)
      h : ∀ û → B (D̃ α ⊢[ û ])
      h (t , t≤α) =
        D̃ γ ⊢≈[ same-stage (≤≤ α≤γ t≤α) (≤≤ (≤≤ α≤γ t≤α) (≤refl (ιᶻ t))) ]

    joinRank : ∀ {α β} {x : D̃ α /≈} {y : D̃ β /≈}
             → Colim D [ α , x ≈ β , y ]
             → ∀ {γ} (rx≤γ : rankD x ≤ γ) (ry≤γ : rankD y ≤ γ)
             → D.hom (box rx≤γ) (plift≈ x) ≡ D.hom (box ry≤γ) (plift≈ y)
    joinRank {x = x} (≈lstage α ≡.refl) rx≤γ ry≤γ = sameHom rx≤γ ry≤γ {x = plift≈ x}
    joinRank {α} {β} {x = x} (≈lstep α≤β x) {γ} rx≤γ ry≤γ = Dα.elimp B h x rx≤γ ry≤γ
      where
      module Dα = SetoidQuotient (D̃ α)
      B : D̃ α /≈ → Prop _
      B x = ∀ {γ} (rx≤γ : rankD x ≤ γ) (ry≤γ : rankD (D.hom (box α≤β) x) ≤ γ)
          → D.hom (box rx≤γ) (plift≈ x)
          ≡ D.hom (box ry≤γ) (plift≈ (D.hom (box α≤β) x))
      h : ∀ û → B (D̃ α ⊢[ û ])
      h û@(t , t≤α) rx≤γ ry≤γ = sameHom rx≤γ ry≤γ {x = D̃ (ιᶻ t) ⊢[ plift û ]}
    joinRank (≈lsym p) rx≤γ ry≤γ = ≡.sym (joinRank p ry≤γ rx≤γ)
    joinRank {x = x} {y = y} (≈ltrans {t = δ , z} p q) {γ} rx≤γ ry≤γ =
      ≡.trans (joinRank p rx≤γ rz≤γ) (joinRank q rz≤γ ry≤γ)
      where
      rz≤γ : rankD z ≤ γ
      rz≤γ = ≡.substp (_≤ γ) (≡.sym (rankColim q)) ry≤γ

    join≈ : ∀ {α β} {x : D̃ α /≈} {y : D̃ β /≈}
          → Colim D [ α , x ≈ β , y ]
          → ∀ {γ} (α≤γ : α ≤ γ) (β≤γ : β ≤ γ)
          → D.hom (box α≤γ) x ≡ D.hom (box β≤γ) y
    join≈ {x = x} {y = y} p {γ} α≤γ β≤γ =
      ≡.trans (toRankHom x α≤γ)
        (≡.trans (joinRank p (≤≤ α≤γ (rankD≤stage x)) (≤≤ β≤γ (rankD≤stage y)))
                 (≡.sym (toRankHom y β≤γ)))

  ϕ-inj≈ : ∀ {t̃ ũ} → (∀ x → Colim D [ ϕ₀ t̃ x ≈ ϕ₀ ũ x ])
         → Colim D^X [ t̃ ≈ ũ ]
  ϕ-inj≈ {α , t̂} {β , û} p =
       α , t̂
    ≈⟨ ≈lstep ∨ᶻ-l t̂ ⟩
       γ , (λ x → D.hom (box ∨ᶻ-l) (t̂ x))
    ≈⟨ ≈lstage (α ∨ᶻ β) (≡.funExt q) ⟩
       γ , (λ x → D.hom (box ∨ᶻ-r) (û x))
    ≈⟨ ≈lsym (≈lstep ∨ᶻ-r û) ⟩
      β , û ∎
    where
    open ≈.≈syntax {S = Colim D^X}
    γ : Z
    γ = α ∨ᶻ β
    q : ∀ x → D.hom (box ∨ᶻ-l) (t̂ x) ≡ D.hom (box ∨ᶻ-r) (û x)
    q x = join≈ (p (lower x)) ∨ᶻ-l ∨ᶻ-r

  ϕ-β : ∀ t̃ x → ϕ (ColimD^X.[ t̃ ]) x ≡ ColimD.[ ϕ₀ t̃ x ]
  ϕ-β t̃ x = ColimD^X.rec-beta
    (λ f̃ → ColimD.[ ϕ₀ f̃ x ])
    (λ p → ColimD.≈[ ϕ-cong p x ])
    t̃

  ϕ-inj : ∀ {t̃ ũ} → (∀ x → ϕ t̃ x ≡ ϕ ũ x) → t̃ ≡ ũ
  ϕ-inj {t̃} {ũ} = ColimD^X.elimp₂ {B = λ t̃ ũ → (∀ x → ϕ t̃ x ≡ ϕ ũ x) → t̃ ≡ ũ} step t̃ ũ
    where
    step : ∀ t̃ ũ → (∀ x → ϕ (ColimD^X.[ t̃ ]) x ≡ ϕ (ColimD^X.[ ũ ]) x) → ColimD^X.[ t̃ ] ≡ ColimD^X.[ ũ ]
    step t̃ ũ p = ColimD^X.≈[ ϕ-inj≈ q ]
      where
      q : ∀ x → Colim D [ ϕ₀ t̃ x ≈ ϕ₀ ũ x ]
      q x = ColimD.effectiveness _ _ eq
        where
        eq : ColimD.[ ϕ₀ t̃ x ] ≡ ColimD.[ ϕ₀ ũ x ]
        eq = ≡.trans (≡.sym (ϕ-β t̃ x)) (≡.trans (p x) (ϕ-β ũ x))

  ϕ-surj≈ : (f̂ : X → Colim₀ D) → ∃ λ t̂ → ∀ x → Colim D [ ϕ₀ t̂ x ≈ f̂ x ]
  ϕ-surj≈ f̂ = ∣ t̂ , p ∣ 
    where
    α : Z
    α = Z.sup (ιˢ s , λ x → f̂ x .proj₁)
    t̂ : Colim₀ D^X
    t̂ = α , (λ (lift x) → D.hom (box (child≤ s _ x)) (proj₂ (f̂ x)))
    p : ∀ x → Colim D [ ϕ₀ t̂ x ≈ f̂ x ]
    p x = sym (≈lstep β≤α (proj₂ (f̂ x)))
      where
      β : Z
      β = f̂ x .proj₁
      β≤α : β ≤ α
      β≤α = child≤ s _ x
      open ≈.≈syntax {S = Colim D}
      open Setoid (Colim D)

  sect : Colim/≈ D → Colim₀ D
  sect = ColimD.rec sect₀ stable
    where
    sect₀ : Colim₀ D → Colim₀ D
    sect₀ (α , s̃) = rankD s̃ , plift≈ s̃
    sect-hom : ∀ {α β} → (p : α ≤ β) → (s̃ : D̃ α /≈) → sect₀ (α , s̃) ≡ sect₀ (β , D.hom (box p) s̃)
    sect-hom {α} {β} p s̃ =
      rankD s̃ , plift≈ s̃
        ≡⟨ ≡.Σ≡ rankD-hom plift≈-hom ⟩
      rankD (D.hom (box p) s̃) , plift≈ (D.hom (box p) s̃) ∎
      where
      open ≡.≡-Reasoning
      rankD-hom : rankD s̃ ≡ rankD (D.hom (box p) s̃) 
      rankD-hom = {!!}
      plift≈-hom : subst D̃/≈ rankD-hom (plift≈ s̃) ≡ (plift≈ (D.hom (box p) s̃)) 
    stable : ∀ {x y} → Colim D [ x ≈ y ] → sect₀ x ≡ sect₀ y
    stable {α , s̃} {α , t̃} (≈lstage α ≡.refl) = ≡.refl
    stable {α , s̃} {β , t̃} (≈lstep p s̃) = sect-hom p s̃
    stable {α , s̃} {β , t̃} (≈lsym p) = ≡.sym (stable p)
    stable {α , s̃} {β , t̃} (≈ltrans p q) = ≡.trans (stable p) (stable q)

  -- Possibly easier double-negated form to see if I can crack it this way.
  ϕ-surj' : (f : X → Colim/≈ D) → ¬ (∀ t̃ → ϕ t̃ ≢ f)
  ϕ-surj' f u = {!!}

  ϕ-surj'' : (f : X → Colim/≈ D) → ∃ λ t̃ → ∀ x → ColimD^X.map (Colim D) (λ f → ϕ₀ f x) (λ p → ϕ-cong p x) t̃ ≡ f x
  ϕ-surj'' f = ∣ {!!} , {!!} ∣
    where
    γ : X → Z
    γ x = rankC (f x)
    α = Z.sup (ιˢ s , γ)
    


  ϕ-surj : (f : X → Colim/≈ D) → ∃ λ t̃ → ϕ t̃ ≡ f
  ϕ-surj f = ∣ {!!} , {!!} ∣

  lemma : Colim/≈ D^X ≅ (X → Colim/≈ D)
  lemma = Bijection→Iso ϕ ((λ p → ϕ-inj (≡.funExt⁻ p)) , ϕ-surj)
